# Maintainer: Mario Finelli <mtfinelli at gmail dot com>

pkgname=linux-dash
pkgver=1.0
pkgrel=1
pkgdesc="A drop-in, low-overhead monitoring web dashboard for a linux machine."
arch=('any')
url="https://github.com/afaqurk/linux-dash"
license=('MIT')
source=(https://github.com/afaqurk/linux-dash/archive/$pkgver.tar.gz)
sha256sums=('117df440c9dcbae980533b12979483a7ef1ec6c77a52f559728cd4eb7351543b')

package(){
  cd "$srcdir/$pkgname-$pkgver"

  # apache config stolen from adminer:
  cat > apache.example.conf <<EOF
  Alias /${pkgname} "/usr/share/webapps/${pkgname}"
  <Directory "/usr/share/webapps/${pkgname}">
    AllowOverride All
    Options FollowSymlinks
    Require all granted
    php_admin_value open_basedir "/srv/:/tmp/:/usr/share/webapps/:/etc/webapps:/usr/share/pear/"
  </Directory>
EOF

  #license
  install -Dm0644 LICENSE.md "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

  #doc
  install -Dm0644 README.md "$pkgdir"/usr/share/doc/"$pkgname"/README.md
  install -Dm0644 README-german.md "$pkgdir"/usr/share/doc/"$pkgname"/README-german.md
  install -Dm0644 README-pt.md "$pkgdir"/usr/share/doc/"$pkgname"/README-pt.md
  mkdir -p "$pkgdir"/usr/share/doc/"$pkgname"/screenshots
  cp -r screenshots/* "$pkgdir"/usr/share/doc/"$pkgname"/screenshots/

  # TODO: check if apache installed first
  install -Dm0644 apache.example.conf "$pkgdir"/etc/httpd/conf/extra/httpd-"$pkgname".conf

  _install_dir="$pkgdir"/usr/share/webapps/"$pkgname"
  install -Dm0644 index.html "$_install_dir"/index.html
  install -Dm0644 favicon.ico "$_install_dir"/favicon.ico
  install -Dm0644 module.php "$_install_dir"/module.php
  mkdir -p "$_install_dir"/css "$_install_dir"/js "$_install_dir"/libs "$_install_dir"/templates
  cp -r css/* "$_install_dir"/css/
  cp -r js/* "$_install_dir"/js/
  cp -r libs/* "$_install_dir"/libs/
  cp -r templates/* "$_install_dir"/templates/

  # set permissions
  find "$_install_dir" -type d -exec chmod 0755 {} \;
  find "$_install_dir" -type f -exec chmod 0644 {} \;

}
