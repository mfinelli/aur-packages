# Maintainer: Mario Finelli <mtfinelli at gmail dot com>

pkgname=linux-dash
pkgver=0.5
pkgrel=1
pkgdesc="A drop-in, low-overhead monitoring web dashboard for a linux machine."
arch=('any')
url="https://github.com/afaqurk/linux-dash"
license=('MIT')
source=('https://github.com/afaqurk/linux-dash/archive/0.5.tar.gz')
md5sums=('2e3398af7098731436368b4fb7eaced6')

package(){
  cd "$srcdir/$pkgname-$pkgver"

  # apache config stolen from adminer:
  cat > apache.example.conf <<EOF
  Alias /${pkgname} "/usr/share/webapps/${pkgname}"
  <Directory "/usr/share/webapps/${pkgname}">
    AllowOverride All
    Options FollowSymlinks
    Require all granted
    php_admin_value open_basedir "/srv/:/tmp/:/usr/share/webapps/:/etc/webapps:/usr/share/pear/"
  </Directory>
EOF

  #license
  install -Dm0644 LICENSE.md "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

  #doc
  install -Dm0644 README.md "$pkgdir"/usr/share/doc/"$pkgname"/README.md
  install -Dm0644 linux-dash.PNG "$pkgdir"/usr/share/doc/"$pkgname"/linux-dash.png

  # TODO: check if apache installed first
  install -Dm0644 apache.example.conf "$pkgdir"/etc/httpd/conf/extra/httpd-"$pkgname".conf

  _install_dir="$pkgdir"/usr/share/webapps/"$pkgname"
  install -Dm0644 index.html "$_install_dir"/index.html
  install -Dm0644 favicon.ico "$_install_dir"/favicon.ico
  mkdir -p "$_install_dir"/css "$_install_dir"/js "$_install_dir"/sh
  cp -r css/* "$_install_dir"/css/
  cp -r js/* "$_install_dir"/js/
  cp -r sh/* "$_install_dir"/sh/

  # set permissions
  find "$_install_dir" -type d -exec chmod 0755 {} \;
  find "$_install_dir" -type f -exec chmod 0644 {} \;

}
