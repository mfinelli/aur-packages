# Maintainer: Mario Finelli <mario at finel dot li>

_gemname=fakefs
pkgname=ruby-${_gemname}
pkgver=1.3.2
pkgrel=1
pkgdesc="A fake filesystem for ruby tests"
arch=(any)
depends=(ruby)
checkdepends=(ruby-bundler ruby-rake ruby-rspec ruby-bump)
makedepends=(git rubygems ruby-rdoc)
url=https://github.com/fakefs/fakefs
license=(MIT)
options=(!emptydirs)
source=(git+https://github.com/fakefs/fakefs.git?tag=v${pkgver})
sha256sums=('SKIP')

prepare() {
  cd $_gemname
  sed -i 's|~>|>=|g' ${_gemname}.gemspec
  sed -i "s/require 'bump\/tasks'//" Rakefile
  sed -i '/namespace :bump/,+5d' Rakefile
  sed -i '/bump/d' ${_gemname}.gemspec
}

build() {
  cd $_gemname
  gem build ${_gemname}.gemspec
}

check() {
  cd $_gemname
  # rake test
  rake spec
}

package() {
  cd $_gemname
  local _gemdir="$(gem env gemdir)"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "$pkgdir/$_gemdir" \
    -n "$pkgdir/usr/bin" \
    $_gemname-$pkgver.gem

  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"

  install -Dm0644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 README.md "$pkgsir/usr/share/doc/$pkgname/README.md"
  install -Dm0644 CHANGELOG.md "$pkgsir/usr/share/doc/$pkgname/CHANGELOG.md"
}

# vim: set ts=2 sw=2 et:
