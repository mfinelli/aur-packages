# Maintainer: Mario Finelli <mario at finel dot li>

_gemname=librarianp
pkgname=ruby-$_gemname
pkgver=1.0.0
pkgrel=2
pkgdesc="A Framework for Bundlers, used by librarian-puppet"
arch=(any)
url=https://github.com/voxpupuli/librarian
license=(MIT)
depends=(ruby ruby-thor)
checkdepends=(ruby-rake ruby-rspec ruby-fakefs)
makedepends=(git rubygems ruby-rdoc)
options=(!emptydirs)
source=(git+https://github.com/voxpupuli/librarian.git?tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd librarian
  sed -i 's|~>|>=|g' ${_gemname}.gemspec
}

build() {
  cd librarian
  gem build ${_gemname}.gemspec
}

check() {
  cd librarian
  rake spec
}

package() {
  cd librarian
  local _gemdir="$(gem env gemdir)"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "$pkgdir/$_gemdir" \
    -n "$pkgdir/usr/bin" \
    $_gemname-$pkgver.gem

  rm -rf "$pkgdir/$_gemdir/cache"

  install -Dm0644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm0644 CHANGELOG.md "$pkgdir/usr/share/doc/$pkgname/CHANGELOG.md"
}

# vim: set ts=2 sw=2 et:
