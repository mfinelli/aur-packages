# Maintainer: Mario Finelli <mario at finel dot li>

_gemname=mini_magick
pkgname=ruby-$_gemname
pkgver=4.11.0
pkgrel=2
pkgdesc="Manipulate images with minimal use of memory via ImageMagick"
arch=(any)
url=https://github.com/minimagick/minimagick
license=(MIT)
depends=(ruby imagemagick)
checkdepends=(ruby-bundler ruby-rspec ruby-rake ruby-guard ruby-guard-rspec ruby-posix-spawn ruby-webmock ruby-nenv ruby-shellany ruby-notiffany ruby-rexml)
makedepends=(rubygems ruby-rdoc)
options=(!emptydirs)
source=(https://github.com/minimagick/minimagick/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('7894bf348fffc9fe3ba855c30a14c21cded9913a1dd3b7fce29d055f91d30c98')

prepare() {
  cd minimagick-${pkgver}
  sed -i 's|~>|>=|g' ${_gemname}.gemspec
}

build() {
  cd minimagick-${pkgver}
  gem build ${_gemname}.gemspec
}

check() {
  cd minimagick-${pkgver}
  rake
}

package() {
  cd minimagick-${pkgver}
  local _gemdir="$(gem env gemdir)"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "$pkgdir/$_gemdir" \
    -n "$pkgdir/usr/bin" \
    $_gemname-$pkgver.gem

  rm -rf "$pkgdir/$_gemdir/cache"

  install -Dm0644 MIT-LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm0644 CHANGELOG.md "$pkgdir/usr/share/doc/$pkgname/CHANGELOG.md"
}

# vim: set ts=2 sw=2 et:
