# Maintainer: Mario Finelli <mario at finel dot li>

_gemname=maxitest
pkgname=ruby-${_gemname}
pkgver=3.6.0
pkgrel=2
pkgdesc="Minitest + all the features you always wanted"
arch=(any)
depends=(ruby ruby-minitest)
checkdepends=(ruby-bundler ruby-rake ruby-rspec)
makedepends=(git rubygems ruby-rdoc)
url=https://github.com/grosser/maxitest
license=(MIT)
options=(!emptydirs)
source=(git+https://github.com/grosser/maxitest.git?tag=v${pkgver})
sha256sums=('SKIP')

prepare() {
  cd $_gemname
  rm Gemfile.lock

  sed -i '/byebug/d' Gemfile
  sed -i '/bump/d' Gemfile
  sed -i '/wwtd/d' ${_gemname}.gemspec
#   sed -i '/bundler/d' spec/cases/helper.rb
#   echo '$:.unshift File.expand_path("../lib", __FILE__)' >> spec/spec_helper.rb
#   echo '--require spec_helper' > .rspec
#   sed -i "s/require 'maxitest\autorun'/require_relative '..\/..\/lib\/maxitest\/autorun'/" spec/cases/helper.rb
}

build() {
  cd $_gemname
  gem build ${_gemname}.gemspec
}

check() {
  cd $_gemname
  rspec spec/
}

package() {
  cd $_gemname
  local _gemdir="$(gem env gemdir)"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "$pkgdir/$_gemdir" \
    -n "$pkgdir/usr/bin" \
    $_gemname-$pkgver.gem

  rm -rf "$pkgdir/$_gemdir/cache"

  install -Dm0644 MIT-LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 Readme.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

# vim: set ts=2 sw=2 et:
