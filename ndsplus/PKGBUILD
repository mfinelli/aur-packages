# Maintainer: Mario Finelli <mtfinelli at gmail dot com>

pkgname=ndsplus-git
pkgver=r8.93f517c
pkgrel=1
pkgdesc="Linux support for the EMS NDS Adapter+."
arch=('i686' 'x86_64')
url="https://github.com/Thulinma/ndsplus"
license=('GPL')
depends=('pkg-config' 'libusb')
makedepends=('git')
provides=('ndsplus')
conflicts=('ndsplus')
source=('git://github.com/Thulinma/ndsplus.git'
        'ndsadapterplus.rules')
md5sums=('SKIP'
         '99666c9d2f9712e7904b0c853c2fcc36')

pkgver() {
  cd "$srcdir/ndsplus"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build(){
  cd "$srcdir/ndsplus"
  make
}

package(){
  cd "$srcdir"
  install -Dm0644 ndsadapterplus.rules "$pkgdir"/etc/udev/rules.d/ndsadapterplus.rules
  cd "ndsplus"
  install -Dm0755 ndsplus "$pkgdir"/usr/bin/ndsplus
}
