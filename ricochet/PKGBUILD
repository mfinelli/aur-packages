# Maintainer: Mario Finelli <mtfinelli at gmail dot com>
# Contributor: Boohbah <boohbah at gmail dot com>
# Contributor: Mikkel Kroman <mk at maero dot dk>
pkgname=ricochet
pkgver=1.0.3
pkgrel=2
pkgdesc="Anonymous peer-to-peer instant messaging."
arch=('i686' 'x86_64')
url="https://ricochet.im/"
license=('custom')
depends=('tor' 'qt5-quick1' 'qt5-quickcontrols' 'qt5-declarative')
makedepends=('qt5-tools')
source=(https://github.com/ricochet-im/ricochet/archive/v$pkgver.tar.gz)
md5sums=('943efd8dc1bfc8dc78c3936ade35e09d')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  qmake CONFIG+=release DEFINES+=RICOCHET_NO_PORTABLE
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  INSTALL_ROOT="${pkgdir}" make install
}
