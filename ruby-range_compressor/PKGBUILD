# Maintainer: Mario Finelli <mario at finel dot li>

_gemname=range_compressor
pkgname=ruby-${_gemname}
pkgver=1.0.0
pkgrel=1
pkgdesc="A micro-gem to compress Arrays of Objects to Arrays of Ranges"
arch=(any)
depends=(ruby ruby-sorted_set)
checkdepends=(ruby-bundler ruby-rake ruby-rspec)
makedepends=(git rubygems ruby-rdoc)
url=https://github.com/jaynetics/range_compressor
license=(MIT)
options=(!emptydirs)
source=(git+https://github.com/jaynetics/range_compressor.git?tag=v${pkgver})
sha256sums=('SKIP')

prepare() {
  cd $_gemname
  echo "require 'sorted_set'" | cat - lib/range_compressor.rb > tmp
  mv tmp lib/range_compressor.rb
}

build() {
  cd $_gemname
  gem build ${_gemname}.gemspec
}

check() {
  cd $_gemname
  rake spec
}

package() {
  cd $_gemname
  local _gemdir="$(gem env gemdir)"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "$pkgdir/$_gemdir" \
    -n "$pkgdir/usr/bin" \
    $_gemname-$pkgver.gem

  rm -rf "$pkgdir/$_gemdir/cache"

  install -Dm0644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

# vim: set ts=2 sw=2 et:
