# Maintainer: Mario Finelli <mario at finel dot li>

_gemname=sshkit
pkgname=ruby-$_gemname
pkgver=1.21.2
pkgrel=2
pkgdesc="Toolkit for deploying code and assets to servers"
arch=(any)
url=https://github.com/capistrano/sshkit
license=(MIT)
depends=(ruby ruby-net-scp ruby-net-ssh)
checkdepends=(ruby-bundler ruby-rake ruby-danger ruby-minitest ruby-minitest-reporters ruby-rainbow ruby-mocha)
makedepends=(git rubygems ruby-rdoc)
options=(!emptydirs)
source=(git+https://github.com/capistrano/sshkit.git?tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd ${_gemname}
  sed -i 's|~>|>=|g' ${_gemname}.gemspec
}

build() {
  cd ${_gemname}
  gem build ${_gemname}.gemspec
}

check() {
  cd ${_gemname}
  rake test:units
  # TODO: get `rake test:functional` working with vagrant
}

package() {
  cd ${_gemname}
  local _gemdir="$(gem env gemdir)"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "$pkgdir/$_gemdir" \
    -n "$pkgdir/usr/bin" \
    $_gemname-$pkgver.gem

  rm -rf "$pkgdir/$_gemdir/cache"

  install -Dm0644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm0644 CHANGELOG.md "$pkgdir/usr/share/doc/$pkgname/CHANGELOG.md"
}

# vim: set ts=2 sw=2 et:
