# Maintainer: Mario Finelli <mario at finel dot li>

_gemname=devtools
pkgname=ruby-${_gemname}
pkgver=0.1.26
pkgrel=1
pkgdesc="A metagem wrapping development tools"
arch=(any)
depends=(ruby)
makedepends=(git rubygems ruby-rdoc)
url=https://github.com/rom-rb/devtools
license=(MIT)
options=(!emptydirs)
source=(git+https://github.com/rom-rb/devtools.git?tag=v${pkgver})
sha256sums=('SKIP')

# prepare() {
#   cd $_gemname
#   sed -i 's|~>|>=|g' ${_gemname}.gemspec
#   sed -i "s/require 'bump\/tasks'//" Rakefile
#   sed -i '/namespace :bump/,+5d' Rakefile
#   sed -i '/bump/d' ${_gemname}.gemspec
# }

build() {
  cd $_gemname
  gem build ${_gemname}.gemspec
}

check() {
  cd $_gemname
  rspec
}

package() {
  cd $_gemname
  local _gemdir="$(gem env gemdir)"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "$pkgdir/$_gemdir" \
    -n "$pkgdir/usr/bin" \
    $_gemname-$pkgver.gem

  rm -rf "$pkgdir/$_gemdir/cache"

  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

# vim: set ts=2 sw=2 et:
