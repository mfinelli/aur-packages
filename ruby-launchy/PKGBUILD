# Maintainer: Mario Finelli <mario at finel dot li>

_gemname=launchy
pkgname=ruby-$_gemname
pkgver=2.5.0
pkgrel=1
pkgdesc="A helper for launching cross-platform applications in a fire and forget manner"
arch=(any)
url=https://github.com/copiousfreetime/launchy
license=(custom:ISC)
depends=(ruby ruby-addressable)
checkdepends=(ruby-rake ruby-minitest)
makedepends=(rubygems ruby-rdoc)
options=(!emptydirs)
source=(https://github.com/copiousfreetime/launchy/archive/v${pkgver}.tar.gz)
sha256sums=('8e2eac27fb392c54ff6f0898c0d6c1f516f5a47cd47f1278fe8e2f50d96f0ed3')

prepare() {
  cd $_gemname-$pkgver
  sed -i '/simplecov/d' ${_gemname}.gemspec
  sed -i '/simplecov/d' spec/spec_helper.rb
  sed -i '/simplecov/d' tasks/default.rake
}

build() {
  cd "$_gemname-$pkgver"
  gem build ${_gemname}.gemspec
}

check() {
  cd "$_gemname-$pkgver"
  rake test
}

package() {
  cd "$_gemname-$pkgver"
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "$pkgdir/$_gemdir" \
    -n "$pkgdir/usr/bin" \
    $_gemname-$pkgver.gem

  rm -rf "$pkgdir/$_gemdir/cache"

  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm0644 HISTORY.md "$pkgdir/usr/share/doc/$pkgname/CHANGELOG.md"
}

# vim: set ts=2 sw=2 et:
